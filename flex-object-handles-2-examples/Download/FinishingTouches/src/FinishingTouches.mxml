<?xml version="1.0" encoding="utf-8"?>
<!--
  This example extends the previous DecoratorExample and adds in some finishing touches to turn it into more of a real
  application.

	a) A toolbar that lets us set the color of objects
	b) Change the delete button to actually delete the selected items
	c) Clear the selection when the user clicks on the background	
  
  Look below for the comments starting with "Step 1", "Step 2", etc. to see what we changed.  

-->

<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" 
				layout="absolute" 
				creationComplete="init()" 
				xmlns:local="*">
				
	<mx:Script>
		<![CDATA[
			import com.roguedevelopment.objecthandles.decorators.AlignmentDecorator;
			import com.roguedevelopment.objecthandles.decorators.DecoratorManager;
			import com.roguedevelopment.objecthandles.constraints.MaintainProportionConstraint;
			import com.roguedevelopment.objecthandles.constraints.SizeConstraint;
			import com.roguedevelopment.objecthandles.constraints.MovementConstraint;
			import com.roguedevelopment.objecthandles.ObjectHandlesSelectionManager;
			import com.roguedevelopment.objecthandles.HandleRoles;
			import com.roguedevelopment.objecthandles.HandleDescription;
			import mx.core.IUIComponent;
			import mx.graphics.ImageSnapshot;
			import mx.events.DragEvent;
			import mx.core.DragSource;
			import mx.managers.DragManager;
			import mx.events.CollectionEvent;
			import mx.events.CollectionEventKind;
			import com.roguedevelopment.objecthandles.ObjectHandles;
			
			protected var dataModel:DataModel = new DataModel();
			[Bindable] protected var controller:Controller = new Controller( dataModel );
			
			
			
			[Bindable] protected var objectHandles:ObjectHandles;
			
			protected var textHandles:Array;
			
			protected var rectangleConstraints:Array = [];
			protected var carConstraints:Array = [];
			protected var textConstraints:Array = [];
			
			protected var decoratorManager:DecoratorManager;
			
			protected var decoratorLayer:Sprite;
			  
			  
			
			
			/**
			 * Starts the drag operation when the mouse is down on our preview component
			 * on the left.
			 * 
			 **/
			protected function beginDrag(event:MouseEvent, shapeType:String) : void
			{
				var sourceImage:Object = event.target;
				if(! sourceImage ) return;
				
				var source:DragSource = new DragSource();
				
				switch( shapeType )
				{
					case "rectangle":					
						source.addData("rectangle","shape"); 
						source.addData(new Point(event.localX,event.localY), "offset");
						DragManager.doDrag(rectangleSource,source,event );
						break;
					case "circle":					
						source.addData("circle","shape"); 
						source.addData(new Point(event.localX,event.localY), "offset");
						DragManager.doDrag(circleSource,source,event );
						break;
					case "car1":					
						source.addData("image","shape"); 
						source.addData("car1.png","sourceImage");
						source.addData(new Point(event.localX,event.localY), "offset");
						DragManager.doDrag(car1Source,source,event );
						break;
					case "car2":					
						source.addData("image","shape");
						source.addData("car2.png","sourceImage"); 
						source.addData(new Point(event.localX,event.localY), "offset");
						DragManager.doDrag(car2Source,source,event );
						break;
					case "text1":										
						source.addData("text","shape");
						source.addData(false,"scaleText");
						source.addData(new Point(event.localX,event.localY), "offset");
						DragManager.doDrag(text1source,source,event );
						break;						
					case "text2":										
						source.addData("text","shape");
						source.addData(true,"scaleText");
						source.addData(new Point(event.localX,event.localY), "offset");
						DragManager.doDrag(text2source,source,event );
						break;						
				}
			}
			
			/**
			 * Allow the object to be dropped onto the mainCanvas object.
			 * 
			 * Note, we should probably check the event.dragSource to make sure we
			 * can handle this type of object.
			 **/
			protected function onDragEnter(event:DragEvent):void
			{
				
				DragManager.acceptDragDrop( mainCanvas );						
			}

			/**
			 * Actually drops the object onto the mainCanvas.
			 * 
			 **/
			protected function onDragDrop(event:DragEvent):void
			{
				
				var offset:Point = event.dragSource.dataForFormat("offset") as Point; // First, grab the offset the mouse was on the preview
				var shapeType:String = event.dragSource.dataForFormat("shape") as String;
				
				switch( shapeType )
				{
					case "rectangle":							
						controller.addSquare( event.localX - offset.x, event.localY - offset.y, 100 , 100 ); // Then actually create the new rectangle taking into account that offset.
						break;
					case "circle":
						controller.addCircle(event.localX - offset.x, event.localY - offset.y, 100 , 25 );
						break;
					case "image":
						var sourceImage:String = event.dragSource.dataForFormat("sourceImage") as String;
						controller.addImage(event.localX - offset.x, event.localY - offset.y, 100 , 25 , sourceImage );
						break;
					case "text":
						var scaleText:Boolean = event.dragSource.dataForFormat("scaleText") as Boolean;
						controller.addText(event.localX - offset.x, event.localY - offset.y, 100 , 25 , scaleText );
						break;
				}
			}
			

			protected function deleteSelectedItems():void
			{
				/*
				  Step 5
				  
				  Update this method to delete the currently selected items.
				  See Controller.as for step 6
				  
				  Note: we also changed the name from deleteSquare to deleteSelectedItems				  
				 */
				controller.deleteItems(objectHandles.selectionManager.currentlySelected);
				
				decoratorManager.updateNow(); 
			}			
			
			protected function linkItems() : void
			{
				controller.linkItems(objectHandles.selectionManager.currentlySelected);
				decoratorManager.updateNow(); // Force a redraw of the decorators.
			}
			
			/**
			 * Called from the creationComplete event (see the root MXML tag)
			 * 
			 * This method sets up our ObjectHandles instance and adds a listener to our data model.
			 **/
			protected function init() : void
			{
				/*
				  Since this is a View object, it should be watching the data model and updating itself
				  when it changed.  This MXML file is tasked with creating or removing display objects when
				  they're added or removed from the data model.  To do that, we'll create an event listener
				  on dataModel.rectangles
				*/
				dataModel.shapes.addEventListener(CollectionEvent.COLLECTION_CHANGE, onShapesChanged );


				decoratorLayer = new Sprite();
				mainCanvas.rawChildren.addChild( decoratorLayer );
				
				objectHandles 	= 	new ObjectHandles( mainCanvas );
								
				decoratorManager = new DecoratorManager( objectHandles, decoratorLayer );
				decoratorManager.addDecorator( new AlignmentDecorator() );			
				decoratorManager.addDecorator( new LinkedDecorator() );

				var movementConstraint:MovementConstraint = new MovementConstraint();
				movementConstraint.minX = 0;
				movementConstraint.minY = 0;
				movementConstraint.maxX = mainCanvas.width ;
				movementConstraint.maxY = mainCanvas.height;
				
				objectHandles.addDefaultConstraint( movementConstraint );				
				objectHandles.addMultiSelectConstraint( movementConstraint );
				
				
			  
			    var sizeConstraint:SizeConstraint = new SizeConstraint();
			    sizeConstraint.minWidth = 20;
			    sizeConstraint.maxWidth = 300;
			    sizeConstraint.minHeight = 10;
			    sizeConstraint.maxHeight = 300;			    
			    				
				rectangleConstraints.push( sizeConstraint );
				
				
				
				
				var aspectRatioConstraint:MaintainProportionConstraint = new MaintainProportionConstraint();				
				carConstraints.push( aspectRatioConstraint );
				
				
				 
				 var textConstraint:TextObjectConstraint = new TextObjectConstraint();
				textConstraints.push(textConstraint);
				
				/*				 
				 				 
				 Create a custom set of handles by copying the default set, and then
				 adding on a MOVE handle.
				*/
				textHandles = objectHandles.defaultHandles.concat();
				textHandles.push( new HandleDescription( HandleRoles.MOVE, new Point(50,50), new Point(0,0)) );
			}
			
			protected function onShapesChanged( event:CollectionEvent ) : void
			{
				switch( event.kind  )
				{
					case CollectionEventKind.ADD:	addShapes( event.items );
													break;				
					case CollectionEventKind.REMOVE: removeShapes(event.items);
												     break;
					default:				
						/* For this simple application, we're only going to deal with adding and removing rectangles,
						   later examples will get more involved. */
				}
			}
			
			/**
			 * Removes rectangles from the display. 
			 * 
			 * This gets called when the model is changed and thigns are removed
			 * from dataModel.rectangles.
			 **/
			protected function removeShapes( shapes:Array ) : void
			{
				
				// Note, since ImageModel, and CircleModel descend from RectangleModel, we don't need
				// to special case this loop.  Im a more complex application, you might want a base
				// class or interface that explictly defines shapes on the screen.
				
				for each ( var rect:RectangleModel in shapes ) // the data models are passed in, so loop over them
				{
					// First, get the view object that corresponds to that data model
					var viewObject:IEventDispatcher = objectHandles.getDisplayForModel( rect );
					
					// Unregister from ObjectHandles so it doesn't try to modify it anymore, this
					// will also remove event listeners.
					objectHandles.unregisterComponent( viewObject );
					
					// Finally, actually remove it from the view.
					mainCanvas.removeChild( viewObject as DisplayObject );
					
				}
			}
			
			
			
			/**
			 * adds rectangles to the display. 
			 * 
			 * This gets called when the model is changes and thigns are added
			 * to dataModel.rectangles.
			 **/			
			protected function addShapes( shapes:Array ) : void
			{
				for each ( var shape:Object in shapes )
				{					
					
					if( shape is ImageModel )
					{
						addImageView( shape as ImageModel );
					}
					else if(shape is CircleModel )
					{
						addCircleView( shape as CircleModel );
					}
					else if( shape is TextModel )
					{
						addTextView( shape as TextModel );						
					}
					else if( shape is RectangleModel )
					{
						addRectangleView( shape as RectangleModel )
					}										
				}
			}
			
			
			
			protected function addTextView( text:TextModel ) : void
			{
				
				var newView:TextView = new TextView();
				newView.dataModel = text;
				
				/*
				  Here we're using a third parameter to registerComponent.  This
				  specifies a non-standard set of handles to add to this type of
				  object.
				  
				  When dealing with text, we want to be able to use the mouse to
				  select the text.  Because of that, we don't want the text area
				  to move around whenever we mouse-down.  By adding a MOVE handle
				  it tells ObjectHandles to move the component when that handle is
				  manipulated instead of when we click on the background of the object.
				  
				  See init() above to see how we create that textHandles variable.
				
				*/
				objectHandles.registerComponent( text, newView , textHandles,  true, textConstraints);								
				mainCanvas.addChild( newView );			
			}
			
			/*
			  Step 8
			  
			  Write the handler for the background clicks to clear the current selection.
			*/
			protected function onBackgroundClick(event:Event):void
			{
				if( event.target != mainCanvas ) return; // remember, click events bubble so we have to check this.
				
				objectHandles.selectionManager.clearSelection();
			}
			
			protected function addRectangleView( rect:RectangleModel ) : void
			{
				var newView:RectangleView2 = new RectangleView2();
				newView.dataModel = rect;
				objectHandles.registerComponent( rect, newView , null, true, rectangleConstraints);
				mainCanvas.addChild( newView );
			}
			
			protected function addCircleView( circle:CircleModel ) : void
			{
				var newView:CircleView = new CircleView(circle);		// This is a pure AS3 object instead of an MXML object and it's data model
																		// is passed into it's constructor.  		
				objectHandles.registerComponent( circle, newView);
				mainCanvas.addChild( newView );	
			}
			
			protected function addImageView( image:ImageModel ) : void
			{
				var newView:ImageView = new ImageView();
				newView.dataModel = image;
				objectHandles.registerComponent( image, newView  , null, true, carConstraints);
				mainCanvas.addChild( newView );				
			}			
			
		]]>
	</mx:Script>
	
	
	<!-- Step 7
	   Add a click handler for the background.
	 -->
	<mx:Canvas left="109" 
			right="0" top="0" 
			bottom="50" 
			backgroundColor="#FFFFFF" 
			id="mainCanvas"
			dragEnter="onDragEnter(event)"
			dragDrop="onDragDrop(event);"
			click="onBackgroundClick(event)"
			/>
		   
	<mx:Image source="rectangle.png" top="10" left="5" mouseDown="beginDrag(event,'rectangle')" id="rectangleSource" />
	<mx:Image source="circle.png" top="40" left="5" mouseDown="beginDrag(event,'circle')" id="circleSource" />
	<mx:Image source="car1.png" top="70" left="5" mouseDown="beginDrag(event,'car1')" id="car1Source" />
	<mx:Image source="car2.png" top="100" left="5" mouseDown="beginDrag(event,'car2')" id="car2Source" />

	<mx:Image source="text1.png" top="130" left="5" mouseDown="beginDrag(event,'text1')" id="text1source" />
	<mx:Image source="text2.png" top="160" left="5" mouseDown="beginDrag(event,'text2')" id="text2source" />
	
	<mx:Button x="10" bottom="35" label="Link Items" width="91" click="linkItems()"/>
	<mx:Button x="10" bottom="10" label="Delete Items" width="91" click="deleteSelectedItems()"/>
	
	<!-- Step 1 
	   Add a toolbar to our UI.
	   
	   Notice that we're passing in our selection manager and controller.  There's a few things we need to do now.
	   
	    a) We need a way of storing the background color for each on screen object (See RectangleModel.as for step 2)
	    b) Each view class needs to respect that background color (See RectangleView.as, RectangleView2.mxml, CircleView.as or ImageView.as for step 3 - they're all pretty similar.  The image one is a little weird)
	    c) Then we need a toolbar that lets us edit that color see Toolbar.mxml for step 4)
	    
	-->
	<local:Toolbar left="109" right="0" bottom="0" selectionManager="{objectHandles.selectionManager}" controller="{controller}" />
</mx:Application>
